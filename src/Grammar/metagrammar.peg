%name Pegasus
%start grammar

grammar             = _ header rules

########### Directives ##########

header              = header_directive*
header_directive    = name_directive
                    | start_directive
                    | extends_directive
                    | ws_directive
                    | ci_directive

name_directive      = ~'%name' _ identifier
start_directive     = ~'%start' _ identifier
extends_directive   = ~'%extends' _ identifier
ws_directive        = ~'%whitespace' _ ~'=' _ unattributed
ci_directive        = ~'%case_insensitive' _

rule_directive      = ~`%lexical` _     <= LexicalDirective
                    | ~`%inline` _      <= InlineDirective

########### Rules ##########

rules           = rule+
rule            = rule_directive* (identifier ~'=' _ <= RuleName) expression

##### Decorator expressions #####

quantifier      = /(?> ([*+?]) | (?: \{ (\d+) (?:,(\d*))? \} ) )/ _
token           = ~'@' prefixable
skip            = ~'~' prefixable
assert          = ~'&' prefixable
not             = ~'!' prefixable
labeled         = /[a-zA-Z_]\w*/ ~':' labelable

##### Terminal expressions #####

reference       = identifier !'='
back_reference  = ~'$' identifier
super_call      = ~`super` (~'::' identifier)?

literal         = / (["']) ( (?:\\.|(?!\1).)* ) \1 / _
word_literal    = ~'`' /[^`]+/ ~'`' _
regexp          = / \/ ((?:\\.|[^\/])*) \/ ([imsuUX]*) / _
eof             = `EOF` _
epsilon         = `E` _
fail            = `FAIL` _

##### Expression components #####

unattributed    = unattributed ~'|' _ terms                    <= OneOf
                | terms

expression      = expression ~'|' _ attributed                 <= OneOf
                | attributed

attributed      = attributed? ~'<=' _ identifier               <= NodeAction
                | attributed_terms

attributed_terms    = attributed term                          <= Sequence
                    | terms

terms           = terms term                                   <= Sequence
                | term

term            = fail
                | labeled
                | labelable

labelable       = prefixed
                | prefixable

prefixed        = token
                | skip
                | assert
                | not

prefixable      = prefixed
                | suffixable
                | primary

suffixable      = suffixed
                | primary

suffixed        = suffixable quantifier

primary         = parenthesized
                | atom

parenthesized   = ~'(' _ expression ~')' _

atom            = eof
                | epsilon
                | fail
                | literal
                | word_literal
                | regexp
                | back_reference
                | super_call
                | reference

identifier      = /[a-zA-Z_]\w*/ _

##### Whitespace & comments #####

%inline _               = ~(ws | comment)*
%inline comment         = /\#[^\n]*/
%inline ws              = /\s+/
