@import JSON from "./json.peg"

@grammar Json5 extends JSON

# A superset of JSON, allowing comments, trailing commas, naked identifiers and more
# @see: https://spec.json5.org/

json5       = super::json
members     = pair (COMMA pair)* COMMA?
elements    = value (COMMA value)* COMMA?
pair        = key COLON value
key         = string | identifier
identifier  = IDENT _
string      = STRING _
number      = (nan | hex | float | integer | infinity) _

nan         = `NaN`
hex         = /0[Xx][0-9A-Fa-f]+/
integer     = %(int expo?)
float       = %(
                int frac expo?
                | /[+-]?/ frac expo?
                | int "."
              )
infinity    = / [+-]? Infinity /

@inline int         = / [+-]? (?: [1-9]\d* | 0(?!\d) )/
@inline frac        = /\.\d+/
@inline expo        = /[eE][+-]?\d+/

@inline COMMA = ~"," _
@inline COLON = ~":" _

@inline STRING  = / (["']) ( (?: \\. | (?!\1)[^\n] )* ) \1 /s
@inline IDENT   = / [a-zA-Z_$] [\w$]* /

_ = ~(WS | COMMENT | LINE_COMMENT)*
@inline WS = /\s+/
@inline COMMENT = "/*" (!"*/" .)* "*/"
@inline LINE_COMMENT = "//" (!"\n" .)* "\n"
